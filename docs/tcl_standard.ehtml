<document "Tcl Coding Standard">

<hrule>

<contents>

<hrule>

<preface changelog "Change Log">

<changelog>
<change 10/27/2014 Update whd>
Moved to Kite.
</change>

<change 8/20/2014 Update whd>
Updated for Mars 3.0.x.
</change>

<change 11/18/10 Update whd>
Removed NaturalDocs commenting conventions.
</change>

<change 11/10/09 New whd>
Updated from the JNEM Tcl coding standard.  Updates include:<p>

<ul>
<li> NaturalDocs commenting conventions
</ul>
</change>

</changelog>

<hrule>

<section intro "Introduction">

This page contains the Tcl/Tk coding standards for Kite.  Projects
using Kite are welcome to follow the same standards.<p>

All new work done on Kite and related projects should follow these
standards, with the following exceptions:<p>

<ul>
  <li> Where conformance to some other standard is clearly
       appropriate. For example, when using an external framework library
       with its own naming conventions, it is appropriate to modify these
       standards so as to be consistent with the practices of the 
       framework.<p>

  <li> Where, in the judgement of the programmer, not following the
       standard in a particular case results in manifestly clearer
       code. This is not a license to flout the standard; it is
       intended to cover special circumstances, and must be defensible
       during code reviews.<p>
</ul>

<section intro.base "Base Standard">

The Kite Tcl/Tk Coding Standard has evolved over time.  Originally,
as the JNEM coding standard, it was a collection of chnages to 
the <i><link http://www.tcl.tk/doc/styleGuide.pdf "Tcl Style Guide"></i>.
This document was once the recognized standard for Tcl code within the
Tcl/Tk community at large.  It was developed at Sun Microsystems at the
beginning of the Tcl 8.x era, and is extremely dated.<p>

<hrule>

<section form "Code Layout">

This section deals with the general appearance of code at the lines of
code level.

<section form.linelength "Line Length">

In general, lines of code should not exceed 80 characters; where
possible, lines should be broken after about 72 characters.<p>

<section form.linelength.longstrings "Long Text Strings">

An exception can be made for long literal
text strings which define a <b>single</b> line of text.  For example, prefer
code like this<p>

<example>
error "This is a very long text string which is a single line of text...."
</example>

to this<p>

<example>
set msg "This is a very long text string "
append msg "which is a single line of text...."
error $msg
</example>

Before letting the string extend past 80 columns, however, first try
to put it on its own line, outdenting if necessary:<p>

<example>
error \
"This is a very long text string which is a single line of text...."
</example>

Long text strings which contain multiple lines of text should be
spread across multiple lines, possibly using
<xref stringx(n) outdent> to remove
or preserve indentation.  For example, the following code defines a long
message, indented with the code; the <xref stringx(n) outdent> command
removes the unwanted whitespace at the beginning of each line:<p>

<example>
puts [outdent {
    Usage: myscript files....

    myscript(1) randomizes the content of the files listed on
    its command line at the byte level, saving the randomized
    content back to the original file with no backup.  The
    system clock time is used as the random seed; hence, given
    precise knowledge of the system clock time at the start of
    processing and a detailed knowledge of the algorithm, it
    might be possible to reconstruct the original content.
}]
</example>

<section form.indentation "Indentation">

Tab characters shall <b>not</b> to be used for indentation.<p>

Text at file scope should be flush with the left margin:<p>

<example>
proc myproc {} {
    ...
}
</example>

Text within curly braces should be indented four spaces
relative to the line on which the open brace appears:<p>

<example>
proc myproc {x} {
    if {$x == 1} {
        # Do this
    } else {
        # Do that
    }
}
</example>

  <li> Continuation lines should be indented four spaces relative to the
       first line in the command.<p>

<example>
label .mylabel \
    -text       "Hello, World" \
    -foreground black          \
    -background white
</example>

Continuations to a bracketed command should usually
be indented four spaces relative to the bracketed command.<p>

<example>
set a [list \
           "This item"       \
           "That item"       \
           "The other item"]
</example>

<section form.continuation "Continuation Lines">

In Tcl, commands which continue onto another line need to be escaped
with a backslash ("\") at the end of the previous line.<p>

Avoid using backslashed continuation lines unnecessarily, except for
certain cases listed below.<p>

Prefer the natural continuation provided by curly brackets to
using backslashes.  For example, <b>never</b> use a backslash
to break a line right before a curly brace.<p>

<example>
# Don't do this!
if {$x == $y} \
{
    puts $x
}
</example>

When a command ends with a homogeneous list of items which is
too long to fit on one line, consider putting each item on its
own line.<p>

<example>
set a [list \
           "This very long item"            \
           "That even longer item"          \
           "The other extremely long item"]
</example>

In this case the backslashes on the second and subsequent lines
should be made to line up, as shown, and should be at least one
space past the end of the longest item.<p>

When creating an object (e.g., a Tk widget) with one or more
creation options, put each option and its value on its own
line.<p>

<example>
label .mylabel \
    -text       "Hello, World" \
    -foreground black          \
    -background white
</example>

The options, values, and backslashes should line up as shown.<p>

Format the command this way even if only one or two options are
being set and the options and values would all fit on the first
line.<p>

 <example>
 # Don't do this!
 label .mylabel -text "Hello, World"
 </example>

The previous rule generally applies to any command which takes
a long string of options.<p>

<section form.comments "Comments">

Good comments are a must.  Comments are used to structure code modules
so that they are easier to read and specific content is easier to find.
At the line-of-code level, comments should focus on details which are
<b>not</b> obvious from looking at the code.  Comments should protect
the programmers who follow from nasty surprises.<p>

The commenting standards contained in the following subsections
should be observed as far as they go; but do not hesitate to extend
them in exceptional circumstances.<p>

<section form.comments.file "File Header Comments">

Every code file should begin with a header comment which looks
generally like this (different kinds of code file may tweak this
comment in different ways, or have additional boilerplate):<p>

<example>
         1         2         3         4         5         6         7  
123456789012345678901234567890123456789012345678901234567890123456789012
#-----------------------------------------------------------------------
# TITLE:
#   tclfile.tcl
#
# PROJECT:
#   Name of the project to which this package belongs
#
# AUTHOR:
#   The name(s) of the author(s).
#
# DESCRIPTION:
#   A description of the contents of this file.
#
#-----------------------------------------------------------------------
</example>

Additional keywords and documentation can be added as needed; content
following each keyword should be indented three spaces from the "#" mark.
The separator lines should be exactly 72 columns wide.  (An editor
which supports templates can be extremely useful here.)<p>

<section form.comments.filesection "File Section Comments">

Code files should be broken into sections using section comments,
which provide a title for the section:<p>

<example>
         1         2         3         4         5         6         7  
123456789012345678901234567890123456789012345678901234567890123456789012
#-----------------------------------------------------------------------
# Sample Section
</example>

If the section requires a longer description, it should follow
directly after the section title, separated by a blank comment line.<p>

<example>
#-----------------------------------------------------------------------
# Sample Section
#
# Here is a longer description of what this section is for,
# what's in it, and so forth.
</example>

<section form.comments.typesection "Type and Widget Sections">

Snit type, Snit widget, TclOO class, and TclOO object definitionsshould 
also be broken into sections. In this case, the section comment should be
indented, and shortened so that the row of hyphens still extends to the 
72nd column:<p>

<example>
         1         2         3         4         5         6         7  
123456789012345678901234567890123456789012345678901234567890123456789012
snit::type mytype {
    #-------------------------------------------------------------------
    # Constructor
    ...
}
</example>

The following sections are commonly used, though rarely all in the
same type or widget definition.  The precise sections will depend
on the content of the definition.<p>

<example>
snit::type mytype {
    #-------------------------------------------------------------------
    # Type Components

    #-------------------------------------------------------------------
    # Type Variables

    #-------------------------------------------------------------------
    # Type Constructor

    #-------------------------------------------------------------------
    # Public Type Methods

    #-------------------------------------------------------------------
    # Private Type Methods

    #-------------------------------------------------------------------
    # Options

    #-------------------------------------------------------------------
    # Instance Variables

    #-------------------------------------------------------------------
    # Constructor

    #-------------------------------------------------------------------
    # Public Methods

    #-------------------------------------------------------------------
    # Private Methods

    #-------------------------------------------------------------------
    # Utility Methods and Procs
}
</example><p>

Section headers used for collections of methods and type methods are
fairly fluid; in particular, the groupings implied by the section
titles above should not be considered part of the standard.  If a
public method is implemented in part by one or two private methods
which exist only for that purpose, they should generally follow
directly after the public method.<p>

In short, think of the code file as a document that's telling a story, and
use the grouping that best communicates the story.<p>

<section form.comments.endline "End-line Comments">

In general, full-line comments are preferred to end-line comments.
It is allowable, though, to use end-line comments to document a
sequence of items such as variable declarations:<p>

<example>
variable thisVariable    5    ;# This short description
variable thatVariable    6    ;# That short description
variable anotherVariable 7    ;# Another short description              
</example>

If the items can't be cleanly documented as shown--e.g., if one
variable name or value is much longer than the
others--then full-line comments should be used instead.<p>

When end-line commands are used, they should always be written
as shown, with the semicolon immediately preceding the pound sign,
"<b>;#</b>".

<section form.comments.events "Notifier Event Comments">

If a module sends <xref notifier(n)> events, they should be
documented in a section comment near the top of the module, or
in the module header.<p>

<section form.comments.option "Option Comments">

Options defined in a Snit type or widget definition should be placed
in an Options section, and preceded by a comment like this:<p>

<example>
snit:type {
    #-------------------------------------------------------------------
    # Options
    ...
    # -myoption
    #
    # Description of my option

    option -myoptions \
        -default "My Value" ...
}
</example>

The description need not include the default value (if any) since it's
right there.<p>

If the option references a <code>-configuremethod</code> or
<code>-cgetmethod</code>, the method may be defined following the
option.  If multiple options reference the same method, it should be
defined at the end of the Options section.<p>

<section form.comments.variable "Variable Comments">

As a general rule, scalar type and instance variables should be
documented with end-line comments unless there is more to be said
than will fit.  Thus, prefer this:<p>

<example>
    #-------------------------------------------------------------------
    # Type Variables

    typevariable tv1     ;# Something about the variable
    typevariable tv2     ;# Something about the variable
    ...

    #-------------------------------------------------------------------
    # Instance Variables

    variable v1          ;# Something about the variable
    variable v2          ;# Something about the variable
</example>

Particularly significant type and instance variables should be preceded by a
header comment, like this:<p>

<example>
snit:type {
    # myvar
    #
    # Description of myvar

    typevariable myvar {...}

    # instvar
    #
    # Description of instvar

    variable instvar {...}
}
</example>

Array and dictionary variables with a variety of distinct keys should 
always be documented using a header comment; the comment should
list the distinct keys or kinds of key, and the kind of values that
go with them.<p>

<example>
    # info array
    #
    # An array of information about the state of the simulation.  The
    # keys are as follows.
    #
    #   dbloaded - 1 if a gramdb(5) is loaded, and 0 otherwise
    #   dbfile   - Name of the loaded gramdb(5) file, or ""
    #              if none.
    
    typevariable info -array {
        dbloaded   0
        dbfile     ""
    }
</example>


<section form.comments.procheader "Procedure Header Comments">

Every procedure definition should begin with a comment that describes
the procedure's calling sequence, arguments, options, purpose, and return
value, along with anything else the developer should know about the
procedure.  The following template should be used:<p>

<example>
# procname arg1 arg2
#
# arg1 - description of arg1
# arg2 - description of arg2
#
# Description of the proc, including the return value.
# The first sentence should be a good summary of the purpose of the
# proc.
</example>

The argument names and descriptions should be separated by a hyphen,
as shown.  When there are multiple arguments, the hyphens should be
vertically aligned, one space to the left of the descriptions.<p>

For example,

<example>
# double x
#
# x - A numeric value
#
# Multiplies x by 2 and returns the result.

proc double {x} {
    ...
}
</example>

The calling sequence should indicate optional arguments using question
marks, as is done in the standard Tcl man pages.<p>

<example>
# multiply x ?factor?
#
# x       - A numeric value
# factor  - A numeric value; defaults to 2
#
# Multiplies x by the factor and returns the result.

proc multiply {x {factor 2}} {
    ...
}
</example>

Repeating arguments should be indicated using an ellipsis ("...").  If
the command takes options, the options and their meanings should be
listed after the normal arguments.<p>

<example>
# modify x ?options...?
#
# x - A numeric value
#
# Options:
#   -multiplyby factor  - Multiply x by the factor
#   -add value          - Add the value to x.
#
# Modifies x as indicated by the options, which are processed in the
# order of appearances.  Returns the modified value.

proc modify {x args} {
    ...
}
</example>

<section form.comments.constructor "Constructor Header Comments">

Constructor header comments are just like procedure headers
except that, falling as they do within a type or widget definition,
they are indented four spaces.

<example>
snit::type mytype {
    ...
    # constructor a b
    #
    # a - A parameter
    # b - Another parameter
    #
    # Creates an instance of mytype given inputs a and b.

    constructor {a b} {
        ...
    }
}
</example>

If the constructor simply handles the type or widget's options,
there's no need to call attention to the <i>args</i> parameter.<p>

If need be, the destructor can be documented in the same way:<p>

<example>
    # destructor
    #
    # Cleans up such-and-such.

    destructor {
        ...
    }
</example>

<section form.comments.methodheader "Method Header Comments">

Method and typemethod header comments are just like procedure headers
except that, falling as they do within a type or widget definition,
they are indented four spaces.  The type or instance command is
omitted from the calling sequence.

<example>
snit::type mytype {
    ...
    # register tag
    #
    # tag - An identifier string.
    #
    # Registers tag for use with all instances of this type.

    typemethod register {tag} {
        ...
    }

    ...
    
    # modify x
    #
    # x - A numeric value
    #
    # Modifies x as specified by the object's options, and returns the
    # modified value.

    method modify {x} {
        ...
    }
}
</example>

<section form.comments.body "Procedure/Method Body Comments">

Procedure, method, and typemethod bodies should be broken into logical
paragraphs using FIRST/NEXT comments.  Each such comment should
introduce and describe the block of code that follows.<p>

<example>
proc UpdateDatabase {args} {
    # FIRST, Validate the arguments.
    ...

    # NEXT, Update the affected database tables
    ...

    # NEXT, Call the -notifycmd callback to notify any interested
    # parties.
    ...
}
</example>

<ul>
  <li> The block following a FIRST/NEXT comment may be as little as
       one line of code, but will usually be larger.<p>

  <li> Small procedures consisting of only a few lines of code can
       dispense with FIRST/NEXT comments.<p>

  <li> Large loop bodies and then/else clauses should also be broken
       into paragraphs using FIRST/NEXT comments.<p>
</ul>

FIRST/NEXT comments help the body of code to tell its story.<p>

<hrule>

<section code "Code Semantics">

<xref form> dealt with coding standards at the level of appearance;
this section deals with code at the level of meaning.<p>

<section code.naming "Naming Conventions">

This section describes the naming conventions to use in writing
Tcl code.<p>

<section code.naming.camelcase "Prefer Camel Case">

Multi-word names should generally use "Camel Case", e.g.,
"myVariable" rather than "my_variable".  However, there are
exceptions, as called out in the following sections.  In
particular, short multi-word names may use all lowercase with no
separators, provided that the name reads clearly.<p>

<section code.naming.public "Public Names">

A name is public if it is intended for use outside the module or type
definition within which it is defined.<p>

<section code.naming.public.command "Public Command Names">

Public command names should always begin with a lowercase
letter, e.g., <xref control(n) assert>.<p>

Where possible without sacrificing clarity, public command names
should be short, and all lowercase with no separators, e.g.,
<xref stringx(n) lmaxlen>, which is short for "list maximum
length".  This is an extreme case, but it succeeds because the
name is consistent with those of the standard list manipulation
commands.<p>

When defining a family of commands with related names, e.g.,
"matnew", "matadd", "matmul", consider defining an ensemble
command instead, e.g., "mat new", "mat add", "mat mul".<p>

If a code file defines a single ensemble command, the command
name should match the file name.  (See
<xref file>.)<p>


<section code.naming.public.methods "Public Methods and Typemethods">

As with public commands, public method names begin with a lowercase
letter.  Because method and typemethod names are always subcommands of
an ensemble command, they are already read as the second word of a
multiword name.  Consequently they are almost always short, consisting
of one or two words, all lowercase with no separators.<p>

When defining a family of methods with related names, consider using
hierarchical method names instead, i.e., unite them in a
sub-ensemble.  An example is the Tk text widget's "tag *" command and
its subcommands.<p>

<section code.naming.public.variables "Public Variables">

Public variables (those documented for use outside the module in which
they are defined) should have names beginning with a lowercase
letter, e.g., "::mymodule::myVariable".<p>

When referenced from another module, public variables should
always be fully qualified, e.g.,<p>

<example>
if {$::mymodule::my == 2} {
    ...
}
</example>

However, use of public variables is discouraged; procedural
interfaces are preferred.<p>

<section code.naming.private "Private Names">

A name is private if it is intended for use only inside the module
within which it is defined.<p>

<section code.naming.private.commands "Private Command Names">

A command is private if it is only intended for use inside the module
within which it is defined.<p>

Private command names always begin with an upper-case letter, and are
frequently much longer than public names, e.g.,
"ComputeMeanSumOfSquares".<p>

<section code.naming.private.methods "Private Methods and Typemethods">

Private method and typemethod names are defined just like private
command names: Camel Case with an initial capital letter, and
frequently with multiple words.<p>

<section code.naming.private.variables "Private and Local Variables">

Private variables, including local variables defined in procedure and
method bodies, private package variables, and Snit instance and type
variables, should be defined with Camel Case names beginning with
an initial lowercase letter.<p>

A previous version of this standard called for private variable
names to begin with a capital letter, to distinguish them from public
variable names; but in practice there are extremely few public variable
names.  Consequently, all variables are written with an initial
lowercase letter.<p>

<section code.expressions "Expressions">

When using <b>expr</b> to compute the value of an expression,
the expression should always be enclosed in curly braces:<p>

<example>
set a [expr {$b * $c}]
</example>

If the braces are omitted, both the Tcl
interpreter and the expression parser will interpolate
variables.  This degrades performance and can also be source of
subtle bugs.<p>

Format expressions too long to fit on one line like blocks of
code.  Indentation should indicating grouping.<p>

<example>
set dist [expr {
    $diameter *
    asin(sqrt($sinHalfDlat*$sinHalfDlat +
              cos($lat1)*cos($lat2)*$sinHalfDlon*$sinHalfDlon))
}]
</example>
       
The <i>Tcl Style Guide</i> suggests that expressions should not rely
on operator precedence, but should always be explicitly
parenthesized. However, "*" has higher precedence that "+" or
"-", and they all have higher precedence than "&&" or "||" in
Tcl, C, C++, and Java, and one should assume that the reader
knows this.  Thus, it's OK to write:

<example>
if {3*$x + 5 == 7 && $y == 9} {
    ...
}
</example>

When an <b>if</b> or <b>while</b> condition consists of a
series of subexpressions and'd or or'd together, it is usually best
to put each subexpression on a separate line.<p>

<example>
    if {3*$x + 5 == 7 &&
        2*$z     == 9 &&
        $y       == 9
    } {
        ...
    }
</example>

In this case, the "&&" or "||" operators should line up.<p>

<section code.return "Functional Return Values">

The <i>Tcl Style Guide</i> says to always use the "return" statement
rather than relying Tcl to return the value of the last command
executed in the body of a proc.<p>

In procs or methods of one or two lines, however, it is
acceptable to omit the "return" statement, provided that the
command whose value will be returned is the last comand in the
body of the proc or method, and that the documented purpose of the
proc or method is to compute and return a particular value.
Thus, prefer<p>

<example>
proc double {x} {
    expr {2*$x}
}
</example>

to<p>

<example>
proc double {x} {
    return [expr {2*$x}]
}
</example>
       
This is especially helpful when programming in a more functional
style.<p>

Commands that are called primarily for their side-effects should
always use "return" explicitly if they return a value, regardless of
length of the command.

<section code.typeawareness "Data Type Awareness">

In static languages like C, variables are typed.  In dynamic languages
like Python, variables are untyped but values are typed.  In Tcl,
neither variables nor values are innately typed.  A value can take on
many different types depending on how it is used.  This is possible
because, in Tcl, "everything is a string."<p>

In practice, however, the programmer often knows whether a variable is
supposed to contain an integer, or a floating point number, or a list, or
a string.  The programmer should try to always be aware of the
notional type of each variable and use the appropriate operations for
that type.  Failure to do so can lead to "shimmering", in which Tcl
is constantly having to convert the value's internal representation
from one type to another.  A bad case of shimmering can lead to
serious performance problems.<p>

Therefore,<p>

Use "eq" and "ne" to compare strings for equality rather than
"==" and "!=".<p>

Use <b>llength</b> or <xref listx(n) got> to test for list-emptiness.  
These are right:<p>

<example>
if {[llength $mylist] == 0} {
    ...
}

if {[got $mylist]} {
  ...
}
</example>

This is wrong:<p>
 
<example>
if {$mylist eq ""} {
    ...
}
</example>

Use <b>list</b> and <b>lappend</b> to build lists from
arbitrary data, rather than using string handling routines.<p>

The string representation of a Tcl list is a
whitespace-delimited list of tokens.  It's perfectly all right
to use this fact to type structured data directly into your
program as list literals; for example, the following is a
common idiom for initializing an array variable:<p>

<example>
array set myarray {
    first   John
    last    Doe
    phone   555-1234
}
</example>

When arbitrary data is involved (i.e., variables), relying on
this is a source of errors.  In the following, list <i>a</i> is
a two-element list, but list <i>b</i> is a three-element list:<p>

<example>
set name1 "Joe"
set name2 "John Paul"

set a "name $name1"
set b "name $name2"
</example>

If <b>list</b> is used, both lists will have two elements, as
desired.<p>

<example>
set name1 "Joe"
set name2 "John Paul"

set a [list name $name1]
set b [list name $name2]
</example>

<section code.callbacks "Callbacks">

Tcl/Tk modules frequently define callback commands which are passed
to other modules to be called at a later time, usually when some
anticipated event occurs.  Three flavors of callback are typically
used:<p>

<ul>
  <li> Script callbacks
  <li> Script callbacks with symbol substitution
  <li> Command prefix callbacks
</ul><p>

Tcl's <b>after</b> command, which is used to define timeout and idle
state callbacks, registers script callbacks.  The callback can be a
Tcl script of any size, from one command to many; <b>after</b> places
no restrictions on it.<p>

Tk's <b>bind</b> command, which is used to define bindings for
keyboard and mouse events, registers script callbacks with symbol
substitution.  The callback can be a Tcl script of any size, and
event-specific "%" symbols, if present, will be replaced with details
of the event.  On a mouse event, for example, <b>%x</b> and <b>%y</b> will be
replaced with the pixel coordinates of the event.<p>

Other Tk callbacks, such as the <b>scrollbar</b> widget's
<b>-command</b> callback, expect a "command prefix": a partial command
to which the scrollbar will <b>lappend</b> one or more additional arguments
before the command is called.<p>

Whatever the object defining the callback allows, however, the client
should always register a callback which consists of a single command
or command prefix.  The command will usually be a
procedure, method, or typemethod defined just for the purpose, along with
any leading arguments required by the client.<p>

<section code.callbacks.procs "Procedure Callbacks">

If the callback command is a normal command or procedure, possibly with 
leading arguments provided by the client, the command should be created 
using <b>list</b>:<p>

<example>
scrollbar .yscroll \
    -orient vertical \
    -command [list .text yview]
</example>

With the exception of Tk widget commands, the callback command should
almost always be fully qualified.<p>

In a Snit type or widget definition, where the callback procedure is
defined in the body of the type, use <b>myproc</b>, <b>mymethod</b>,
or <b>mytypemethod</b> instead of list:<p>

<example>
myobject configure \
    -command [myproc MyCallbackProc arg arg arg...]
</example>

These commands builds the callback command as a list in the same way
that <b>list</b> does, but also fully-qualify the procedure or method
automatically.<p>

<section code.callbacks.events "Event Bindings">

In a Tk event binding, any substitutions should be written as
arguments to the command:<p>

<example>
proc ButtonPress {x y} {
    # Do something
}

# Call ButtonPress when they click on the thingy
bind .thingy <lb>1<rb> [list ButtonPress %x %y]
</example>

Tk event bindings are allowed to return the <b>break</b> return code;
this tells Tk that no further bindings should be called for the given
event.  The event handler should do this by explicitly returning
the <b>break</b> code, rather than by including "break" in the
callback string.  For example, the following is correct:<p>

<example>
proc ButtonPress {x y} {
    # Do something

    # Process no further bindings for this button press
    return -code break
}

# Call ButtonPress when they click on the thingy
bind .thingy <lb>1<rb> [list ButtonPress %x %y]
</example>

Alternatively, the following can be used.<p>

<example>
bind .thingy <lb>1<rb> {ButtonPress %x %y; break}
</example>

The following doesn't work at all; <b>break</b> is called when the
callback is defined, not when the button is pressed:<p>

<example>
bind .thingy <lb>1<rb> [list ButtonPress %x %y; break]
</example>

<section code.callbacks.calling "Calling Callbacks">

Callbacks should almost always be called in the global scope; any
other choice has too many opportunities for unpleasant surprises.
If the callback command takes no additional arguments, it may be
called like this:<p>

<example>
uplevel #0 $callback
</example>

It is frequently the case that the callback might not be defined, as
when it's registered using an object option.  In this case, the code
is:<p>

<example>
if {$callback ne ""} {
    uplevel #0 $callback
}
</example>

If the callback takes additional arguments, the code is a little longer:<p>

<example>
# Call callback with two additional args, a and b
if {$callback ne ""} {
    set cmd $callback
    lappend cmd $a $b
    uplevel #0 $cmd
}
</example>

Finally, note that the <xref control(n) callwith> command does all this
in one line; the following example is equivalent to its predecessor.<p>

<example>
callwith $callback $a $b
</example>

Of course, <xref control(n) callwith> can also be used for callbacks
which take no additional arguments.<p>

Alternatively, if the command prefix might consist of multiple tokens,
"{*}" notation can be used:<p>

<example>
if {$callback ne ""} {
    {*}$callback $a $b
}
</example>

This presumes that the callback command is fully-qualified, as it should
be.<p>

<hrule>

<section file "File-Level Standards">

The <i><xref kdg.html "Kite Developer's Guide"></i> has information on
the expected content and layout of particular kinds of code files.  
The following are a few rules:<p>

<ul>
<li> Library packages should define a package-wide namespace.  The package
     code should be defined within that namespace with public names exported.<p>

<li> Applications should define an application-wide namespace as a container
     for global variables, e.g., <tt ::app_kite::library>.  But in general,
     the global namespace belongs to the application.<p>

<li> Most modules should export a single ensemble command that serves as
     the module's entry point.  Exceptions include collections of small
     entities such as data validation types or utility commands.<p>

     When a single ensemble command is used, it should have the same 
     name as the module file.<p>

<li> Some available Tcl modules (e.g., 
     <link http://www.tcl.tk/man/tcl8.6/TclCmd/http.htm http(n)>) expect 
     users to access their commands using a qualified name, e.g., 
     <tt ::http::geturl>.  New code should use a <tt snit::type> singleton
     or 
     <link http://www.tcl.tk/man/tcl8.6/TclCmd/namespace.htm "namespace ensemble">
     instead.<p>
</ul>

</document>







