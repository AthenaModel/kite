# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    filex.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Tcltest test suite for kiteutils(n) filex.tcl
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n)

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.2 
    eval ::tcltest::configure $argv
}

# Import tcltest(n)
namespace import ::tcltest::*

#-----------------------------------------------------------------------
# Load the package to be tested

source ../../lib/kiteutils/pkgModules.tcl
namespace import ::kiteutils::*

#-------------------------------------------------------------------
# appendfile

test appendfile-1.1 {writes new file to disk} -body {
    set fname "appendfile-1.1.txt"
    set a "my content"
    appendfile $fname $a
    set b [readfile $fname]

    expr {$a eq $b}
} -cleanup {
    removeFile $fname
} -result {1}

test appendfile-1.2 {appends to file} -body {
    set fname "appendfile-1.2.txt"
    set a "my content"
    appendfile $fname $a
    appendfile $fname $a
    set b [readfile $fname]

    expr {$b eq "$a$a"}
} -cleanup {
    removeFile $fname
} -result {1}


#-------------------------------------------------------------------
# readfile

test readfile-1.1 {error, cannot read file} -body {
    readfile noSuchFile.txt
} -returnCodes {
    error
} -match glob -result {*: no such file or directory}

test readfile-1.2 {reads file successful} -setup {
    set a "file contents"
    makeFile $a readfile-1.2.txt
} -body {
    set b [readfile readfile-1.2.txt]
    expr {$b eq "$a\n"}
} -result {1}

#-------------------------------------------------------------------
# writefile

test writefile-1.1 {writes file to disk} -body {
    set fname "writefile-1.1.txt"
    set a "my content"
    writefile $fname $a
    set b [readfile $fname]

    expr {$a eq $b}
} -cleanup {
    removeFile $fname
} -result {1}

test writefile-1.2 {Writes ifchanged} -body {
    set fname "writefile-1.3.txt"
    set a "my content"
    writefile $fname $a
    set t1 [file mtime $fname]
    after 2000
    writefile $fname "$a plus" -ifchanged
    set t2 [file mtime $fname]
    expr {$t1 < $t2}
} -cleanup {
    removeFile $fname
} -result {1}

test writefile-1.3 {Doesn't write when unchanged} -body {
    set fname "writefile-1.3.txt"
    set a "my content"
    writefile $fname $a
    set t1 [file mtime $fname]
    after 2000
    writefile $fname $a -ifchanged
    set t2 [file mtime $fname]
    expr {$t1 < $t2}
} -cleanup {
    removeFile $fname
} -result {0}


 
#-------------------------------------------------------------------
# Cleanup

cleanupTests








