# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    stringx.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Tcltest test suite for kiteutils(n) stringx.tcl
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n)

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.2 
    eval ::tcltest::configure $argv
}

# Import tcltest(n)
namespace import ::tcltest::*
 
#-----------------------------------------------------------------------
# Load the package to be tested

package require kiteutils
namespace import ::kiteutils::*

#-------------------------------------------------------------------
# codeblock

test codeblock-1.1 {empty block} -body {
    codeblock myblock {} {}
    myblock
} -result {}

test codeblock-1.2 {block with text} -body {
    codeblock myblock {} {
        This is my block.
        It has two lines.
    }
    myblock
} -result {This is my block.
It has two lines.}

test codeblock-1.3 {block with args} -body {
    codeblock myblock {owner thing} {
        This is %owner block.
        It has two %thing.
    }
    myblock Joe's boots
} -result {This is Joe's block.
It has two boots.}

test codeblock-1.4 {block with initbody} -body {
    codeblock myblock {owner} {
       set owner [string toupper $owner]
       set len [string length $owner]
    } {
        This is %owner block.
        "%owner" is %len characters long.
    }
    myblock Joe's
} -result {This is JOE'S block.
"JOE'S" is 5 characters long.}

#-------------------------------------------------------------------
# normalize

test normalize-1.1 {strips whitespace} -body {
    normalize "   abc   def \n ghi \t "
} -result {abc def ghi}

#-------------------------------------------------------------------
# outdent

test outdent-1.1 {outdent: empty block} -body {
    outdent {}
} -result {}

test outdent-1.2 {outdent: leading blank line} -body {
    outdent {
              Now that we have this,
              we can make things
              look a lot nicer.}
} -result {Now that we have this,
we can make things
look a lot nicer.}

test outdent-1.3 {outdent: trailing blank line} -body {
    outdent {Now that we have this,
              we can make things
              look a lot nicer.
            }
} -result {Now that we have this,
              we can make things
              look a lot nicer.}
       

#-------------------------------------------------------------------
# Cleanup

cleanupTests






