# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    ehtml.test
#
# PROJECT:
#    athena-kite
#
# DESCRIPTION:
#    kitedocs(n) test suite for ehtml.tcl
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n)

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.2 
    eval ::tcltest::configure $argv
}

# Import tcltest(n)
namespace import ::tcltest::*

 
#-----------------------------------------------------------------------
# Load the package to be tested

source ../../lib/kitedocs/pkgModules.tcl
namespace import ::kitedocs::*
namespace import ::kiteutils::*

#-------------------------------------------------------------------
# Setup

# setup
#
# Set up for tests

proc setup {} {
    macro trans
    trans register ::kitedocs::ehtml 
    trans reset
}

# cleanup
#
# Cleans up after a test.

proc cleanup {} {
    trans destroy
}

# pexpand block
#
# block   - A block of text
#
# Expands macros and pretty prints the result.

proc pexpand {block} {
    set result [trans expand [::kiteutils::outdent $block]]
    return "\n[string trimright $result]\n"
}

    
#-------------------------------------------------------------------
# hrule

test hrule-1.1 {hrule} -setup {
    setup
} -body {
    trans expand {-<hrule>-}
} -cleanup {
    cleanup
} -result {-<p><hr><p>-}

#-------------------------------------------------------------------
# lb

test lb-1.1 {lb} -setup {
    setup
} -body {
    trans expand {-<lb>-}
} -cleanup {
    cleanup
} -result {-&lt;-}

#-------------------------------------------------------------------
# link

test link-1.1 {link} -setup {
    setup
} -body {
    trans expand {-<link foo.html>-}
} -cleanup {
    cleanup
} -result {-<a href="foo.html">foo.html</a>-}

test link-1.2 {link with anchor} -setup {
    setup
} -body {
    trans expand {-<link foo.html bar>-}
} -cleanup {
    cleanup
} -result {-<a href="foo.html">bar</a>-}

#-------------------------------------------------------------------
# nbsp

test nbsp-1.1 {nbsp} -setup {
    setup
} -body {
    trans expand {-<nbsp "a b c">-}
} -cleanup {
    cleanup
} -result {-a&nbsp;b&nbsp;c-}

#-------------------------------------------------------------------
# quote

test quote-1.1 {quote} -setup {
    setup
} -body {
    trans expand {-<quote "<&>">-}
} -cleanup {
    cleanup
} -result {-&lt;&amp;&gt;-}

#-------------------------------------------------------------------
# rb

test rb-1.1 {rb} -setup {
    setup
} -body {
    trans expand {-<rb>-}
} -cleanup {
    cleanup
} -result {-&gt;-}

#-------------------------------------------------------------------
# textToID

test textToID-1.1 {strips whitespace} -setup {
    setup
} -body {
    trans expand {-<textToID "  My, Text&string! ">-}
} -cleanup {
    cleanup
} -result {-my_textstring-}

#-------------------------------------------------------------------
# xref/xrefset

test xref-1.1 {xref/xrefset} -setup {
    setup
    trans eval [list xrefset foo Foo! ./bar.html]
} -body {
    trans expand {-<xref foo>-}
} -cleanup {
    cleanup
} -result {-<a href="./bar.html">Foo!</a>-}

test xref-1.2 {xrefset returns link} -setup {
    setup
} -body {
    trans expand {-<xrefset foo Foo! ./bar.html>-}
} -cleanup {
    cleanup
} -result {-<a href="./bar.html">Foo!</a>-}

test xref-1.3 {unknown xref id} -setup {
    setup
} -body {
    # -docroot not set
    trans expand {-<xref foo(n)>-}
} -cleanup {
    cleanup
} -result {-<b><tt>&lt;xref foo(n)&gt;</tt></b>-}

test xref-1.4 {manref when docroot set to "."} -setup {
    setup
    ehtml configure -docroot "."
    trans reset
} -body {
    trans expand {-<xref foo(n)>-}
} -cleanup {
    cleanup
} -result {-<a href="./mann/foo.html">foo(n)</a>-}

test xref-1.5 {docref when docroot set to "."} -setup {
    setup
    ehtml configure -docroot "."
    trans reset
} -body {
    trans expand {-<xref foo/bar.html "Bar!">-}
} -cleanup {
    cleanup
} -result {-<a href="./foo/bar.html">Bar!</a>-}

#-------------------------------------------------------------------
# HTML Equivalents

test html_equivalent-1.1 {expand to themselves.} -setup {
    setup
} -body {
    pexpand {
        <b>........</b>
        <i>........</i>
        <code>.....</code>
        <tt>.......</tt>
        <em>.......</em>
        <strong>...</strong>
        <sup>......</sup>
        <sub>......</sub>
        <pre>......</pre>
        <ol>.......</ol>
        <ul>.......</ul>
        <li>.......</li>
        <p>........</p>
        <table>....</table>
        <tr>.......</tr>
        <th>.......</th>
        <td>.......</td>
    }
} -cleanup {
    cleanup
} -result {
<b>........</b>
<i>........</i>
<code>.....</code>
<tt>.......</tt>
<em>.......</em>
<strong>...</strong>
<sup>......</sup>
<sub>......</sub>
<pre>......</pre>
<ol>.......</ol>
<ul>.......</ul>
<li>.......</li>
<p>........</p>
<table>....</table>
<tr>.......</tr>
<th>.......</th>
<td>.......</td>
}

test html_equivalent-1.2 {style macros convenience forms} -setup {
    setup
} -body {
    pexpand {
        <b .....some text>
        <i .....some text>
        <code ..some text>
        <tt ....some text>
        <em ....some text>
        <strong some text>
        <sup ...some text>
        <sub ...some text>
        <pre ...some text>
    }
} -cleanup {
    cleanup
} -result {
<b>.....some text</b>
<i>.....some text</i>
<code>..some text</code>
<tt>....some text</tt>
<em>....some text</em>
<strong>some text</strong>
<sup>...some text</sup>
<sub>...some text</sub>
<pre>...some text</pre>
}

test html_equivalent-1.3 {img can have args} -setup {
    setup
} -body {
    trans expand {-<img src="tbd">-}
} -cleanup {
    cleanup
} -result {-<img src="tbd">-}


#-------------------------------------------------------------------
# deflist.../deflist


test deflist-1.1 {definition list} -setup {
    setup
} -body {
    pexpand {
        <deflist>
        <def "The Term">
        Text describing the term.<p>
        <def "Another Term">
        Text describing the term.<p>
        </deflist>
    }
} -cleanup {
    cleanup
} -result {
<dl>
<dt><b>The Term</b></dt>
<dd>
        
Text describing the term.<p>
<dt><b>Another Term</b></dt>
<dd>
        
Text describing the term.<p>
</dl>
}

test deflist-1.2 {def expands term} -setup {
    setup
} -body {
    pexpand {<def "The <i Term>">}
} -cleanup {
    cleanup
} -result {
<dt><b>The <i>Term</i></b></dt>
<dd>
}

#-------------------------------------------------------------------
# topiclist.../topiclist


test topiclist-1.1 {topic list} -setup {
    setup
} -body {
    pexpand {
        <topiclist>
        <topic "The Term">
        Text describing the term.
        </topic>
        <topic "Another Term">
        Text describing the term.
        </topic>
        </topiclist>
    }
} -cleanup {
    cleanup
} -result {
<table class="pretty">
<tr class="header">
<th align="left">Topic</th> 
<th align="left">Description</th>
</tr>
        
<tr class="oddrow" valign="baseline">
<td>The Term</td>
<td>
        
Text describing the term.
</td>
</tr>
        
<tr class="evenrow" valign="baseline">
<td>Another Term</td>
<td>
        
Text describing the term.
</td>
</tr>
        
</table>
}

#-------------------------------------------------------------------
# example.../example

test example-1.1 {example} -setup {
    setup
} -body {
    pexpand {
        <example>
        Some text.
        </example>
    }
} -cleanup {
    cleanup
} -result {
<pre class="example">
Some text.
</pre>
}


#-------------------------------------------------------------------
# listing.../listing

test listing-1.1 {listing} -setup {
    setup
} -body {
    pexpand {
        <listing>
        Some text.
        Some more text.
        Still more text.
        </listing>
    }
} -cleanup {
    cleanup
} -result {
<pre class="listing">
<span class="linenum">0001</span> Some text.
<span class="linenum">0002</span> Some more text.
<span class="linenum">0003</span> Still more text.
</pre>
}

#-------------------------------------------------------------------
# mark/bigmark

test mark-1.1 {mark} -setup {
    setup
} -body {
    trans expand {-<mark A>-}
} -cleanup {
    cleanup
} -result {-<div class="mark">A</div>-}

test mark-1.2 {bigmark} -setup {
    setup
} -body {
    trans expand {-<bigmark A>-}
} -cleanup {
    cleanup
} -result {-<div class="bigmark">A</div>-}


#-------------------------------------------------------------------
# changelog.../changelog


test changelog-1.1 {change log} -setup {
    setup
} -body {
    pexpand {
        <changelog>
        <change 7/9/2014 New will>
        Text describing the change.
        </change>
        </changelog>
    }
} -cleanup {
    cleanup
} -result {
<table class="pretty" width="100%" cellpadding="5" cellspacing="0">
<tr class="header">
<th align="left" width="10%">Status</th>
<th align="left" width="70%">Nature of Change</th>
<th align="left" width="10%">Date</th>
<th align="left" width="10%">Initiator</th>
</tr>
        
<tr class="oddrow" valign=top>
<td>New</td>
<td>
Text describing the change.
</td>
<td>7/9/2014</td>
<td>will</td>
</tr>
        
</table><p>
}

#-------------------------------------------------------------------
# procedure.../procedure


test procedure-1.1 {procedure} -setup {
    setup
} -body {
    pexpand {
        <procedure>
        <step>
        What to do.
        </step/>
        How to do it.
        </step>
        </procedure>
    }
} -cleanup {
    cleanup
} -result {
<table border="1" cellspacing="0" cellpadding="2">
        
<tr valign="top">
<td><b>1.</b></td>
<td>
        
What to do.
</td><td>
How to do it.
</td></tr>
</table>
}


#-------------------------------------------------------------------
# Cleanup

cleanupTests




